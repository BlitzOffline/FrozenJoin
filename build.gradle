import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

group = "com.github.frcsty"
version = "2.1.2"

repositories {
    mavenCentral()
    maven { url = "https://repo.destroystokyo.com/repository/maven-public/" }
    maven { url = "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url = "https://repo.codemc.org/repository/maven-public" }
    maven { url = "https://jitpack.io/" }
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:4.0.6"
    testImplementation "io.kotest:kotest-assertions-core-jvm:4.0.6"
    implementation "me.mattstudios.utils:matt-framework:1.4"
    implementation "org.bstats:bstats-bukkit:1.7"
    implementation("com.github.cryptomorin:XSeries:5.3.1") {
        exclude group: "com.google.code.findbugs", module: "jsr305"
    }
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72") {
        exclude group: "org.jetbrains", module: "annotations"
    }
    compileOnly "org.jetbrains:annotations:19.0.0" // kotlin-stdlib-jdk8 dependency
    compileOnly "net.luckperms:api:5.1"
    compileOnly "com.destroystokyo.paper:paper-api:1.16.1-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.10.9"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: project.version]
    }
}

def path = "com.github.frcsty.frozenjoin.libs"

shadowJar {
    relocate "me.mattstudios.mf", "${path}.mf"
    relocate "org.bstats", "${path}.bstats"
    relocate "com.cryptomorin.xseries", "${path}.xseries"
    relocate "kotlin", "${path}.kotlin"
}

sourceCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
    kotlinOptions.jvmTarget = sourceCompatibility
}
